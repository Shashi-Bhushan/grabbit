apply from: "${rootProject.projectDir}/gradle/verifyComponentConfig.gradle"
apply from: "${rootProject.projectDir}/gradle/utils.gradle"

apply plugin: 'osgi'
apply plugin: 'scr'
apply plugin: 'sling-bundle'
apply plugin: 'protobuf'

// remove "mvn install" since it's not needed and causes a conflict
def installTask = tasks.findByPath('install')
if (installTask != null) tasks.remove(installTask)

apply plugin: 'cqpackage'

ext.bundleName = 'Grabbit'
ext.bundleDescription = 'Grabbit'
ext.symbolicName = "com.twcable.grabbit"

cqPackage.bundleInstallRoot = '/apps/grabbit/install'
cqPackage.nativeStartLevel = 30
cqPackage.dependencyStartLevel = 20

bundle.installPath = '/apps/grabbit/install/${cqPackage.nativeStartLevel}'

//noinspection GroovyAssignabilityCheck
dependencies {
    // Library for Groovy
    compile "org.codehaus.groovy:groovy-all:${groovy_version}"

    // Used as part of the build to create the OSGI-INF/serviceComponents.xml file
    compile "org.apache.felix:org.apache.felix.scr.annotations:${scr_annotations_version}"

    compile "com.day.cq.workflow:cq-workflow-console:${cq_version_workflow_console}"

    //Google libraries
    compile "com.google.guava:guava:${guava_version}"

    //For annotations like @Nonnull, etc.
    compile "com.google.code.findbugs:jsr305:${jsr305_version}"

    // Apache Sling libraries
    compile "org.apache.sling:org.apache.sling.api:${sling_api_version}"
    compile "org.apache.sling:org.apache.sling.jcr.api:${sling_commons_version}"

    // Apache Felix libraries
    compile "org.apache.felix:org.osgi.core:${felix_osgi_version}"
    compile "org.apache.felix:org.osgi.compendium:${felix_osgi_version}"

    compile "joda-time:joda-time:${joda_time_version}"

    // Need the "javax.jms" library for Spring JMS
    compile "org.glassfish:javax.jms:${jms_version}"

    // Working with the JCR
    compile "javax.jcr:jcr:${jcr_version}"
    compile "org.apache.jackrabbit:jackrabbit-jcr-commons:${jackrabbit_version}"

    // Logging
    compile "org.slf4j:slf4j-api:${slf4j_version}"
    compile "org.slf4j:jcl-over-slf4j:${slf4j_version}"
    runtime "ch.qos.logback:logback-classic:${logback_version}"

    // StAX library
    compile "org.codehaus.woodstox:woodstox-core-asl:${woodstox_version}"

    // Servlet/JSP libraries
    compile "javax.servlet:servlet-api:${servlet_api_version}"

    // Spring libraries
    compile "org.springframework.osgi:spring-osgi-core:${spring_osgi_version}"
    compile "org.springframework:org.springframework.context:${spring_version}"
    compile "org.springframework:spring-oxm:${spring_version}"
    compile "org.springframework:org.springframework.jms:${spring_version}"
    compile "org.springframework:org.springframework.jdbc:${spring_version}"
    compile "org.springframework.osgi:spring-osgi-extender:${spring_osgi_version}"

    //This version of Spring Batch brings in transitive dependencies of Spring 3.2.9
    //Though, the Manifest for spring-batch-core and spring-batch-infrastructure
    //Has a range of [3.1.2, 4.0.0). Thus, excluding these transitive dependencies
    compile "org.springframework.batch:spring-batch-core:${spring_batch_version}", {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework", module: "spring-tx"
        exclude group: "com.thoughtworks.xstream", module: "xstream"
    }

    compile "org.apache.commons:commons-lang3:${commons_lang_version}"
    compile "commons-io:commons-io:${commons_io_version}"

    // HTTP components libraries
    compile "org.apache.httpcomponents:httpclient-osgi:${httpcomponents_version}", {
        exclude module: "httpclient"
        exclude module: "httpcore"
        exclude module: "httpclient-cache"
        exclude module: "httpmime"
    }
    compile "org.apache.httpcomponents:httpcore-osgi:${httpcomponents_version}", {
        exclude module: "httpclient-cache"
        exclude module: "httpcore"
        exclude module: "httpcore-nio"
        exclude module: "httpmime"
    }

    compile "org.apache.servicemix.bundles:org.apache.servicemix.bundles.protobuf-java:${servicemix_protobuf_java_version}"
}

// workaround for http://issues.gradle.org/browse/GRADLE-1682
configurations.runtime.exclude group: 'org.apache.felix', module: 'org.osgi.foundation'
configurations.runtime.exclude group: 'org.apache.felix', module: 'javax.servlet'

configurations.compile {
    exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
    // This project's runtime for inclusion in the CQ package
    cq_package project(path: ":${name}", configuration: 'runtime')
    cq_package project(path: ":${name}", configuration: 'default')

    native_cq_package files("${project.jar.archivePath}")
}


jar {
    from ('src/main/content/SLING-INF/content', {
        into 'SLING-INF/content'
    })
}

jar.manifest {

    attributes 'Sling-Initial-Content': project.configFolders.collect {
        "SLING-INF/content/apps/grabbit/${it}; overwrite:=true; uninstall:=true; path:=/apps/grabbit/${it}"
    }.join(', ')

    attributes 'Bundle-Name': project.bundleName
    attributes 'Bundle-SymbolicName': project.symbolicName
    attributes 'Bundle-Description': project.bundleDescription
    instruction 'Import-Package', 'groovy.json; version="[2.3,3.0)"'
    instruction 'Import-Package', 'groovy.json.internal; version="[2.3,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.scope; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.file.transform; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.file; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.configuration.xml; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.configuration.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.explore.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.launch.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.job.flow.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.job.flow.support.state; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository.dao; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.support.transaction; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.beans.factory.config; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.core.io; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.core.task; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop.framework; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop.scope; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.scheduling.concurrent; version="[3.1,4.0)"'
    instruction 'Import-Package', "org.aopalliance.aop;version=1.0.0"
    instruction 'Import-Package', '*'


    // export everything to OSGi except *.impl packages
    instruction 'Export-Package', "!*.impl, *;-noimport:=false;version=${version}"
}

// exclude bundles provided by CQ
configurations.cq_package {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'commons-httpclient', module: 'commons-httpclient'
    exclude group: 'com.sun.xml.bind'
    exclude group: 'aopalliance', module: 'aopalliance'
    exclude group: 'javax.xml.stream'

    // use CQ's logging, not logback
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'

    // excluding Joda Time too as AEM5.6 provides its own in com.day.commons.osgi.wrapper.joda-time
    exclude group: 'joda-time', module: 'joda-time'
    
    exclude group: 'com.google.code.findbugs', module: 'jsr305'

    exclude group: 'com.day.cq.workflow', module: 'cq-workflow-console'

    exclude group: 'org.apache.felix', module: 'org.apache.felix.scr.annotations'

    exclude group: 'org.apache.commons', module: 'commons-lang3'
    exclude group: 'org.apache.commons', module: 'commons-lang'
}

bundle.installPath = '/apps/grabbit/install/${nativeStartLevel}'

idea {
    module {
        // The whole build dir is excluded by default, but we need build/generated-sources,
        // which contains the generated proto classes.
        excludeDirs = [
            file("$buildDir/classes"),
            file("$buildDir/docs"),
            file("$buildDir/dependency-cache"),
            file("$buildDir/libs"),
            file("$buildDir/reports"),
            file("$buildDir/resources"),
            file("$buildDir/test-results"),
            file("$buildDir/tmp"),
        ]
    }
}
